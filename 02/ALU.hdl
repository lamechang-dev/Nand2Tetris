// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
        // if (zx == 1) sets x = 0        // 16-bit constant
        Mux16(a=x , b=false , sel=zx , out=tmpX );
        // if (zy == 1) sets y = 0        // 16-bit constant
        Mux16(a=y , b=false , sel=zy , out=tmpY );
        
        // if (nx == 1) sets x = !x       // bitwise not
        Not16(in=tmpX, out=notTmpX);
        Mux16(a=tmpX , b=notTmpX , sel=nx , out=tmpX2);
        // if (ny == 1) sets y = !y       // bitwise not
        Not16(in=tmpY, out=notTmpY);
        Mux16(a=tmpY, b=notTmpY, sel=ny, out=tmpY2);

        // if (f == 1)  sets out = x + y  // integer 2's complement addition
        // if (f == 0)  sets out = x & y  // bitwise and
        Add16(a=tmpX2, b=tmpY2, out=xPlusY);
        And16(a=tmpX2, b=tmpY2, out=xAndY);
        Mux16(a=xAndY, b=xPlusY, sel=f, out=tmpOut);

        // if (no == 1) sets out = !out   // bitwise not
        Not16(in=tmpOut, out=notOut);
        Mux16(a=tmpOut, b=notOut, sel=no, out=finalOut);
        Mux16(a=finalOut, b=finalOut, sel=false, out[0..7]=out0to7, out[8..15]=out8to15);


        // zr if (out == 0) equals 1, else 0
        Or8Way(in=out0to7, out=lowBitsOr);
        Or8Way(in=out8to15, out=highBitsOr);
        Or(a=lowBitsOr, b=highBitsOr, out=outIsNonZero);
        Not(in=outIsNonZero, out=zr);

        // ng if (out < 0) equals 1, else 0
        And16(a=finalOut , b=finalOut , out[0..14]= tmp, out[15] = tmp2);
        And(a=tmp2, b=true, out=ng);

        Mux16(a=finalOut, b=finalOut, sel=false, out=out);
}
